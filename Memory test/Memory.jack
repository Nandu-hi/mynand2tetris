// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack
/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static Array heap;
    static Array ram;
    static Array freelist;

    /** Initializes the class. */
    function void init() {
        let ram=0;
        let heap=2048;
        let freelist=heap;
        let heap[0]=0;
        let heap[1]=14334;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address]=value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var int curr;
        var int currsize;
        var Array block;

        let curr=freelist;
        let currsize=0;

        while(~(curr=0)){
            let currsize=ram[curr+1];

            if(~(currsize < (size+2))){
                let block=curr + 2 + currsize - size;
                let ram[curr+1]=currsize - size - 2;
                let block[1]=size;
                return block;
            }

            let curr=ram[curr];

        } 
        return 0;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        let ram[o-2]=freelist;
        let freelist=(o-2);
        return;
    }    
}
